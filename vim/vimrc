" Jon Parise <jon@indelible.org>

" Plugins
"
" curl -fLo ~/.vim/autoload/plug.vim --create-dirs \
"   https://raw.githubusercontent.com/junegunn/vim-plug/master/plug.vim
call plug#begin()
Plug 'croaker/mustang-vim'
Plug 'chriskempson/base16-vim'
Plug 'kien/ctrlp.vim'
Plug 'vim-airline/vim-airline' | Plug 'vim-airline/vim-airline-themes'
Plug 'junegunn/goyo.vim'
Plug 'tpope/vim-commentary'
Plug 'tpope/vim-fugitive' | Plug 'tommcdo/vim-fugitive-blame-ext'
Plug 'tpope/vim-projectionist'
Plug 'tpope/vim-repeat'
Plug 'tpope/vim-unimpaired'
Plug 'airblade/vim-gitgutter'
Plug 'justinmk/vim-dirvish'
Plug 'wincent/ferret'
Plug 'wincent/loupe'
Plug 'wincent/terminus'
Plug 'milkypostman/vim-togglelist'
Plug 'majutsushi/tagbar', {'on': 'TagbarToggle'}
Plug 'solarnz/arcanist.vim'
Plug 'elixir-lang/vim-elixir', {'for': 'elixir'}
Plug 'slashmili/alchemist.vim', {'for': 'elixir'}
Plug 'rust-lang/rust.vim', {'for': 'rust'}
Plug 'fatih/vim-go', {'for': 'go'}
Plug 'cespare/vim-toml', {'for': 'toml'}
Plug 'rodjek/vim-puppet', {'for': 'puppet'}
Plug 'solarnz/thrift.vim', {'for': 'thrift'}
Plug 'mitsuhiko/vim-jinja', {'for': 'html'}
Plug 'mustache/vim-mustache-handlebars', {'for': 'html.handlebars'}
Plug 'godlygeek/tabular' | Plug 'plasticboy/vim-markdown', {'for': 'markdown'}
Plug 'junegunn/vader.vim', {'for': 'vader'}
Plug 'jparise/hive.vim', {'for': 'hive'}
Plug 'raimon49/requirements.txt.vim', {'for': 'requirements'}
Plug 'jparise/vim-graphql', {'for': 'graphql'}
Plug 'jvirtanen/vim-cocoapods'
Plug 'kergoth/vim-hilinks'
if has('timers') && (has('nvim') || (exists('*job_start') && exists('*ch_close_in')))
    Plug 'w0rp/ale'
endif
if v:version >= 704
    Plug 'ludovicchabant/vim-gutentags'
endif
call plug#end()

source $VIMRUNTIME/macros/matchit.vim

set autoindent          " turn autoindenting on
set autowrite           " auto-save before running external commands
set background=dark     " dark background
set backspace=indent,eol,start " smarter backspace
set cmdwinheight=3      " height of the command window
set complete=.,w,b,u,t,i,kspell " completion sources
set completeopt=menu,longest,preview " insert mode completion options
set cpoptions=aABceF    " vi compatibility options
set encoding=utf-8      " change the default text encoding
set noerrorbells        " turn off error bells
set fillchars=vert:│    " window separator character
set expandtab           " expand tabs to spaces
set formatoptions+=nr   " options for "text format" ("gp")
set hidden              " enable hidden buffers
set history=100         " remember the last 100 lines of command-line history
set nohlsearch          " turn off search result highlighting
set ignorecase          " mostly ignore case in search patterns. see smartcase
set incsearch           " use incremental search
set noinsertmode        " don't start vim in insertmode
set nojoinspaces        " don't insert two spaces after period when joining lines
set laststatus=2        " always show status line
set lazyredraw          " don't update screen while executing commands
set nolist              " turn off extended character listing
set listchars=tab:»·,extends:›,precedes:‹,nbsp:␣,trail:·
set magic               " use extended regexp in search patterns
set modelines=0         " turn off modelines
set mousehide           " hide the mouse in the gui
set nonumber            " don't number lines
set report=0            " show all changes
set ruler               " show cursor position
set scrolloff=3         " scroll when within 3 lines of the screen's edge
set shiftround          " round indent size to a multiple of shiftwidth
set shiftwidth=4        " number of spaces in each indent
set shortmess=atI       " abbreviate all messages
set showbreak=↪\        " show at the start of wrapped lines
set noshowcmd           " don't show incomplete commands
set showmode            " show the current mode
set showtabline=1       " show tabline as needed
set smartcase           " respect uppercase characters when searching
set smarttab            " smart movement and deleting based on tab settings
set softtabstop=4       " set soft tab stop to four spaces
set splitbelow          " open horizontal splits below current window
set splitright          " open vertical splits to the right
set nostartofline       " don't jump to first char with page commands
set synmaxcol=800       " don't attempt to syntax highlight long lines
set tabstop=4           " set tabs to eight spaces
set textwidth=78        " set the maximum text width
set title               " turn on the titlebar
set updatetime=250      " speed up file sync frequency
set wildchar=<TAB>      " character used for command line expansion
set wildignore+=*/.git/*,*/.hg/*,*.swp,*.o " file patterns to ignore
set wildmenu            " use menu when in 'full' command-line completion mode
set wildmode=list:longest,full " completion mode behavior
set wrapmargin=0        " wrap from the right margin
set nowritebackup       " don't write backups

if exists('&belloff')
    set belloff=all
else
    set visualbell t_vb=
endif

if has('linebreak') && exists('&breakindent')
    set breakindent                 " indent wrapped lines to match start
    if exists('&breakindentopt')
        set breakindentopt=shift:2  " emphasize broken lines by indenting them
    endif
endif

if v:version > 703 || v:version == 703 && has('patch541')
    set formatoptions+=j " remove comment leader when joining comment lines
endif

" Swap files, etc.
if $USER != 'root'
    set directory=~/.cache/vim//,~/tmp//,.
else
    set noswapfile
    set viminfo=
endif

" Platform / GUI options
if has('gui_running')
    colorscheme base16-ocean
    hi VertSplit ctermbg=NONE guibg=NONE
    set guioptions=aAeimgr
    set ruler
    set title

    if has('gui_macvim')
        silent! set guifont=Source\ Code\ Pro:h14
        if &guifont == 'Source Code Pro:h14'
            let g:airline_powerline_fonts = 1
        else
            set guifont=Menlo:h14
        endif
    elseif has('gui_win32')
        set guifont=Consolas:h10.5
    endif
elseif has('termguicolors')
    set termguicolors
    colorscheme base16-ocean
    hi VertSplit ctermbg=NONE guibg=NONE
    let g:airline_powerline_fonts = 1
else
    let g:loaded_airline = 1

    colorscheme mustang
    hi StatusLine term=bold,reverse ctermfg=236 ctermbg=252
    hi Todo ctermfg=9 ctermbg=234 term=standout
endif

let g:TerminusCursorShape = 0

" Autocommands
if has('autocmd')
    augroup VimStartup
        autocmd!

        " Resize all windows when vim itself is resized.
        autocmd VimResized * execute "normal! \<c-w>="

        " When editing a file, always jump to the last known cursor position,
        " unless the position is invalid or we're inside of an event handler.
        autocmd BufReadPost *
        \ if line("'\"") >= 1 && line("'\"") <= line("$") |
        \   execute "normal! g`\"" |
        \ endif
    augroup END
endif

" Avoid unintentional switches to Ex mode.
nmap Q q

" Buffer management
map <Left>  :bprev<CR>
map <Right> :bnext<CR>
nnoremap <leader>d :bp\|bd #<CR>

" Reformat the current paragraph to textwidth
imap <C-J> <c-o>gqap
map  <C-J> gqap

" Toggle the tagbar.
nnoremap <silent> <leader>t :TagbarToggle<CR>

" Toggle the location and quickfix lists with custom mappings
let g:toggle_list_no_mappings = 1
nmap <script> <silent> <leader>l :call ToggleLocationList()<CR>
nmap <script> <silent> <leader>c :call ToggleQuickfixList()<CR>
